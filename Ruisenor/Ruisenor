/*
Tecnol�gico de Costa Rica, Escuela de Computaci�n, Ingenier�a en Computaci�n
Curso de Simulaci�n, I Semestre 2021
Tarea del Ruise�or
Joseph Tenorio Pereira (2019064588) y Alejandro Garc�a Bejarano (2019380047) 

Probabilidad de alcanzar $2500 empezando con $2000 y apostando $100, con estrategia cl�sica: 0.198

Probabilidad de alcanzar $2500 empezando con $2000 y apostando $100, con estrategia cl�sica: 0.603

*/

/*-----------------------------------------C�digo Fuente de la simulaci�n-----------------------------------------*/

/* JUEGO DE BLACKJACK */
newcmd jugarBJ res
	copy 42#1 58#0 bolsa
	sample 1 bolsa res
end

/* ESTRATEGIA CLASICA */
newcmd EstrategiaClasica dineroInicio apuestaInicial gananciaDeseada respuesta
	dineroAcumulado = dineroInicio
	
	while (dineroAcumulado > 0 AND dineroAcumulado < gananciaDeseada)
		jugarBJ partida
		if partida=1
			dineroAcumulado = dineroAcumulado + apuestaInicial
		else
			dineroAcumulado = dineroAcumulado - apuestaInicial
		end
	end

	if(dineroAcumulado <= 0)
		respuesta = 0
	else
		respuesta = 1
	end
end

/* ESTRATEGIA DEL RUISE�OR */
newcmd EstrategiaNightingale dineroInicio apuestaInicial gananciaDeseada respuesta
	dineroAcumulado = dineroInicio
	apuestaEstrategica = apuestaInicial

	while (dineroAcumulado > 0 AND dineroAcumulado < gananciaDeseada)
		jugarBJ partida
		if partida=1 AND dineroAcumulado >= apuestaEstrategica
			dineroAcumulado = dineroAcumulado + apuestaEstrategica
			apuestaEstrategica = apuestaInicial
		else 
			dineroAcumulado = dineroAcumulado - apuestaEstrategica
			if (dineroAcumulado < apuestaEstrategica * 2)
				apuestaEstrategica = dineroAcumulado	
			else
				apuestaEstrategica = apuestaEstrategica * 2
			end
		end
	end
	
	if(dineroAcumulado <= 0)
		respuesta = 0
	else 
		respuesta = 1
	end
end 

/* LIMPIEZA DE LA CONSOLA */
CLEAROUTPUT

/* SIMULACION */
victoriasNightingale = 0
victoriasClasica = 0
numCiclos = 1000000

repeat numCiclos

	/* Rondas con estrategia del clasica */	
	EstrategiaClasica 2000 100 2500 resClasica
	victoriasClasica = victoriasClasica + resClasica

	/* Rondas con estrategia clasica */
	EstrategiaNightingale 2000 100 2500 resNightingale
	victoriasNightingale = victoriasNightingale + resNightingale

end 

/* Calculo e impresion de la probabilidad */

probNightingale = victoriasNightingale / numCiclos
probClasica =  victoriasClasica / numCiclos

print "===== Probabilidad de llegar a $2500 con la estrategia cl�sica ====="
print probClasica
print 
print "===== Probabilidad de llegar a $2500 con la estrategia del ruise�or ====="
print probNightingale




